
module mod_cube_format

  use modinput
  use modmain, only : natmtot, nspecies, natoms, atposc, spzn
  implicit none

  real(8), parameter :: bohr2ang = 0.529177d0
  private bohr2ang

contains

!-------------------------------------------------------------------------------
  subroutine write_3d_cube(fname,label,boxl,igrid,npt,rdata)
    implicit none
    ! input/output
    character*(*) :: fname
    character*(*) :: label
    real(8), intent(in) :: boxl(4,3)
    integer, intent(in) :: igrid(3)
    integer, intent(in) :: npt
    real(8), intent(in) :: rdata(npt)
    ! local
    integer :: is, ia, ip, i, ix, iy, iz
    real(8) :: boxc(4,3)

    ! WRITE A FORMATTED CUBEFILE VERY SIMILAR TO THOSE CREATED BY
    ! THE GAUSSIAN PROGRAM OR THE CUBEGEN UTILITY.
    ! THE FORMAT IS AS FOLLOWS (LAST CHECKED AGAINST GAUSSIAN 98):
    !
    ! LINE   FORMAT      CONTENTS
    ! ===============================================================
    !  1     A           TITLE
    !  2     A           DESCRIPTION OF PROPERTY STORED IN CUBEFILE
    !  3     I5,3F12.6   #ATOMS, X-,Y-,Z-COORDINATES OF ORIGIN
    !  4-6   I5,3F12.6   #GRIDPOINTS, INCREMENT VECTOR
    !  #ATOMS LINES OF ATOM COORDINATES:
    !  ...   I5,4F12.6   ATOM NUMBER, CHARGE, X-,Y-,Z-COORDINATE
    !  REST: 6E13.5      CUBE DATA (WITH Z INCREMENT MOVING FASTEST, THEN
    !                    Y AND THEN X)
    !
    ! FOR ORBITAL CUBE FILES, #ATOMS WILL BE < 0 AND THERE WILL BE ONE
    ! ADDITIONAL LINE AFTER THE FINAL ATOM GIVING THE NUMBER OF ORBITALS
    ! AND THEIR RESPECTIVE NUMBERS. ALSO THE ORBITAL NUMBER WILL BE
    ! THE FASTEST MOVING INCREMENT.
    !
    ! ALL COORDINATES ARE GIVEN IN ATOMIC UNITS.
   
    do i = 1, 4
      call r3mv(input%structure%crystal%basevect, boxl(i,:), boxc(i,:))
    end do

    open(80,file=trim(fname),status='Unknown',action='Write')

    write(80,'(A)') 'Generated by mod_cube_format.f90'
    write(80,'(A)') trim(label)

    write(80,'(I5,3F12.6)') natmtot, boxc(1,:)
    write(80,'(I5,3F12.6)') igrid(3)+1, boxc(4,:)/dble(igrid(3))
    write(80,'(I5,3F12.6)') igrid(2)+1, boxc(3,:)/dble(igrid(2))
    write(80,'(I5,3F12.6)') igrid(1)+1, boxc(2,:)/dble(igrid(1))
    
    do is = 1, nspecies
    do ia = 1, natoms(is)
      write(80,'(I5,4F12.6)') &
      &  abs(int(spzn(is))),  & ! atom number
      &  0.d0,                & ! charge
      &  atposc(:,ia,is)        ! x-, y-, z-coordinate
    end do
    end do

    do ip = 1, npt
      write(80,'(E13.5)', Advance="no") rdata(ip)
      if (mod(ip,6)==0) write(80,*)
    end do

    close(80)
    
    return
  end subroutine
  
end module