#!/bin/csh

set strout="str_hint.out"

if ( ! -e $strout ) then
  set strout="str.out"
endif

if (! -e $strout ) then
  echo str.out or str_hint.out does not exist
  echo NOTE: runstruct_xxxx needs to be run within the subdirectory containing a structure.
  exit 1
endif

set wrapfilename="pwscf.wrap"

set wrapfile="$wrapfilename"
if (! -e $wrapfile) then
  set wrapfile="../$wrapfilename"
  if (! -e $wrapfile) then
    set wrapfile="../../$wrapfilename"
    if (! -e $wrapfile) then
      set wrapfile="../../../$wrapfilename"
      if (! -e $wrapfile) then
        echo You need a $wrapfilename file in $PWD , $PWD/.. , $PWD/../.. or $PWD/../../..
        echo NOTE: $0 needs to be run within the numbered subdirectory.
        exit 1
      endif
    endif
  endif
endif


(  echo "   ibrav="`fixcell -b < $strout | head -1 | awk '{print 1+(index("cP cF cI hP hR tP tI oP oC oF oI mP mC aP",$1)-1)/3;}'` ;\
  (cellcvrt -f < $strout | fixcell -b | tail +3 | head -3 ; echo 1 0 0 ; echo 0 1 0; echo 0 0 1 ) | cellcvrt -f -abc | head -1 | awk '{print "   a="$1; print "   b="$2; print "   c="$3; print "   cosbc="cos($4/360*2*3.1415926535897932384626433); print "   cosac="cos($5/360*2*3.1415926535897932384626433); print "   cosab="cos($6/360*2*3.1415926535897932384626433);}' ;  echo "   nat="`cellcvrt -f < $strout | tail +7 | wc -l` ;\
  echo "   ntyp="`cellcvrt -f < $strout | tail +7 | awk '{print $4}' | sort -u | wc -l` \
) >& pwsystem.tmp

( echo "ATOMIC_POSITIONS (crystal)" ;\
  cellcvrt -f < $strout | tail +7 | awk '{print "   " $4,$1,$2,$3;}' \
) >& pwatom.tmp

grep -q '[kK][pP][pP][rR][aA]' < $wrapfile
if ( $status == 1 ) then
  grep -q INSERTKPOINTS < $wrapfile
  if ( $status == 0 ) then
    echo Missing kppra token in $wrapfile
    exit
  endif
endif
set nbkp=`getvalue '[kK][pP][pP][rR][aA]' < $wrapfile`
set nbat=`cellcvrt -pn < $strout`
( echo "K_POINTS (automatic)" ; \
  echo "   "`( echo $nbkp $nbat | awk '{print $1/$2}' ; cat $strout | cellcvrt -f | head -3 ) | kmesh -q -r` 0 0 0 \
) >& pwkpoint.tmp

awk '{if ($1=="INSERTSYSTEM") {system("cat pwsystem.tmp");} \
      else if ($1=="INSERTATOMS") {system("cat pwatom.tmp");} \
      else if ($1=="INSERTKPOINTS") {system("cat pwkpoint.tmp");} \
      else {print $0;} \
}' $wrapfile | grep -v -i kppra >! pwscf.in

#rm -f pwsysten.tmp pwatom.tmp pwkpoint.tmp
