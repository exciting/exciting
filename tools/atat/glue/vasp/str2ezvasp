#!/bin/csh
# Syntax: str2ezvasp

set wrapfilename="vasp.wrap"
if ( $#argv != 0 ) then
  set wrapfilename="$1"
endif

set strout="str_hint.out"

if ( ! -e $strout ) then
  set strout="str.out"
endif

if (! -e $strout ) then
  echo str.out or str_hint.out does not exist
  echo NOTE: str2ezvasp needs to be run within the subdirectory containing a structure.
  exit 1
endif

set wrapfile="$wrapfilename"
if (! -e $wrapfile) then
  set wrapfile="../$wrapfilename"
  if (! -e $wrapfile) then
    set wrapfile="../../$wrapfilename"
    if (! -e $wrapfile) then
      set wrapfile="../../../$wrapfilename"
      if (! -e $wrapfile) then
        echo You need a $wrapfilename file in $PWD , $PWD/.. , $PWD/../.. or $PWD/../../..
        echo NOTE: str2ezvasp needs to be run within the numbered subdirectory.
        exit 1
      endif
    endif
  endif
endif

cat $wrapfile >! vasp.in
echo "" >>! vasp.in
echo "[POSCAR]" >>! vasp.in
echo "title" >>! vasp.in
echo 1. >>! vasp.in
cat $strout | cellcvrt -c -sig=9 | tail +4 | head -3 >>! vasp.in
echo Cartesian >>! vasp.in
cat $strout | cellcvrt -c -sig=9 | tail +7 >>! vasp.in
